def render-tpl [
    template_file: string
]: record -> string {
    $in | format pattern (open --raw $template_file)
}

def "nur release" [
    version: string
] {
    let main_version = if ("+" in $version) {$version | split row "+" | get 0} else {$version}
    print $"Calculating hash for nur (ansi purple)($version)(ansi reset) files"
    let url = $'https://github.com/nur-taskrunner/nur/archive/refs/tags/v($main_version).tar.gz'
    let hash = (
        http get --raw $url
        | shasum -a 256
        | parse --regex '^(?P<hash>[^ ]+) '
        | get hash
        | first
    )

    print ""
    print $"Rendering template for Formula/nur.rb using version (ansi purple)($main_version)(ansi reset)"
    let warning = ({} | render-tpl "templates/warning.template")
    let formula = (
        {
            version: $version
            main_version: $main_version
            url: $url
            hash: $hash
            bin: "{bin}"  # Workaround for #{bin} used in tests
        }
        | render-tpl "templates/nur.rb.template"
    )

    [$warning, $formula] | str join (char nl) | save -f "Formula/nur.rb"

    print ""
    print $"Creating release commit '(ansi purple)nur ($main_version)(ansi reset)'"
    git add Formula/nur.rb
    git commit -m $"nur ($main_version)"

    print ""
    print $"(ansi yellow)Don't forget to push last commit!(ansi reset)"
}
